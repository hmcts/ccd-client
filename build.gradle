plugins {
    id 'checkstyle'
    id 'io.spring.dependency-management' version '1.1.3'
    id 'org.springframework.boot' version '3.1.3'
    id 'com.github.ben-manes.versions' version '0.47.0'
    id 'se.patrikerdes.use-latest-versions' version '0.2.18'
    id 'java-library'
    id 'maven-publish'
}

def buildNumber = System.getenv("RELEASE_VERSION")?.replace("refs/tags/", "") ?: "DEV-SNAPSHOT"

group 'uk.gov.hmcts.reform'
version buildNumber

checkstyle {
    maxWarnings = 0
    toolVersion = '10.12.3'
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

compileJava {
    options.compilerArgs << '-parameters' << '-Xlint:deprecation'
}

compileTestJava {
    options.compilerArgs << '-Xlint:deprecation'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

jar {
    manifest {
        attributes 'Implementation-Title': project.name, 'Implementation-Version': project.version
    }
}

task printVersion {
    doLast {
        print project.version
    }
}

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "https://www.opensource.org/licenses/MIT"
            distribution "repo"
        }
    }

    scm {
        url "https://github.com/hmcts/ccd-client"
    }
}


task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def gitRepo = "https://github.com/hmcts/ccd-client"

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'Core case data store client'
                description = 'Core case data store client'
                url = gitRepo
                licenses {
                    license {
                        name = 'MIT License, Copyright (c) 2023 HM Courts & Tribunals Service'
                        url = "https://www.opensource.org/licenses/MIT"
                    }
                }
                scm {
                    url = gitRepo
                }
            }
        }
    }
}

ext {
    lombokVersion = '1.18.28'
}

sourceSets {
    functionalTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    functionalTestImplementation.extendsFrom testImplementation
    functionalTestRuntimeOnly.extendsFrom testRuntimeOnly
}

task functionalTest(type: Test) {
    description = 'Runs functional tests.'
    group = 'verification'

    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
}

dependencies {
    implementation(platform("org.springframework.cloud:spring-cloud-dependencies:2022.0.4"))
    implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    implementation group: 'io.github.openfeign', name: 'feign-jackson'

    implementation group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.10.0'

    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    testImplementation group: 'com.github.tomakehurst', name: 'wiremock-standalone', version: '3.0.0'
    testImplementation group: 'com.google.guava', name: 'guava', version: '32.1.2-jre'

    testImplementation group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion

    functionalTestImplementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '5.1.0'
    functionalTestImplementation group: 'com.github.hmcts', name: 'idam-java-client', version: '3.0.1'

    functionalTestImplementation group: 'org.assertj', name: 'assertj-core', version: '3.24.2'
    functionalTestCompileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    functionalTestAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion

    functionalTestImplementation group: 'com.fasterxml.jackson.module', name: 'jackson-module-parameter-names', version: '2.15.2'
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

tasks.withType(Test) {
    useJUnitPlatform()
}

bootJar {
    enabled = false
}

jar {
    enabled = true
    archiveClassifier = ''
}
