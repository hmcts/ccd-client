plugins {
    id 'checkstyle' apply true
    id 'io.spring.dependency-management' version '1.0.3.RELEASE'
    id 'org.springframework.boot' version '1.5.9.RELEASE' apply true
    id 'com.jfrog.bintray' version '1.8.0' apply true
    id 'java-library' apply true
    id 'maven' apply true
    id 'maven-publish' apply true
    id 'net.ltgt.apt' version '0.10' apply true
}

group 'uk.gov.hmcts.reform'
version '2.5.0'

checkstyle {
    maxWarnings = 0
    toolVersion = '8.5'
}

sourceCompatibility = 1.8

compileJava {
    options.compilerArgs << '-parameters' << '-Xlint:deprecation'
}

compileTestJava {
    options.compilerArgs << '-Xlint:deprecation'
}

repositories {
    jcenter()
    mavenLocal()
}

jar {
    manifest {
        attributes 'Implementation-Title': project.name, 'Implementation-Version': project.version
    }
}

task printVersion {
    doLast {
        print project.version
    }
}

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "http://www.opensource.org/licenses/mit-license.php"
            distribution "repo"
        }
    }

    scm {
        url "https://github.com/hmcts/core-case-data-store-client"
    }
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'core-case-data-store-client'
            version project.version

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Core case data store client')
                root.appendNode('name', 'Core case data store client')
                root.appendNode('url', 'https://github.com/hmcts/core-case-data-store-client')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['Main']
    publish = true
    pkg {
        repo = 'hmcts-maven'
        name = 'core-case-data-store-client'
        userOrg = 'hmcts'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/hmcts/core-case-data-store-client'
        version {
            name = project.version
        }
    }
}

ext {
    lombokVersion = '1.16.18'
    jacksonVersion = '2.8.10'
}

dependencies {
    compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-feign', version: '1.4.0.RELEASE'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    compile group: 'io.github.openfeign', name: 'feign-httpclient', version: '9.5.0'
    compile group: 'com.netflix.feign', name: 'feign-jackson', version: '8.18.0'
    
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion
    compile group: 'com.fasterxml.jackson.module', name: 'jackson-module-parameter-names', version: jacksonVersion
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: jacksonVersion
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: jacksonVersion

    compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    apt group: 'org.projectlombok', name: 'lombok', version: lombokVersion

}

bootRepackage {
    enabled = false;
}
