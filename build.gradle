plugins {
    id 'checkstyle' apply true
    id 'io.spring.dependency-management' version '1.0.3.RELEASE'
    id 'org.springframework.boot' version '1.5.9.RELEASE' apply true
    id 'com.jfrog.bintray' version '1.8.0' apply true
    id 'java-library' apply true
    id 'maven' apply true
    id 'maven-publish' apply true
}

group 'uk.gov.hmcts.reform'
version '1.0.0'

checkstyle {
    maxWarnings = 0
    toolVersion = '8.5'
}

sourceCompatibility = 1.8

compileJava {
    options.compilerArgs << '-parameters' << '-Xlint:deprecation'
}

compileTestJava {
    options.compilerArgs << '-Xlint:deprecation'
}

repositories {
    maven {
        url "https://dl.bintray.com/hmcts/hmcts-maven"
    }
    jcenter()
    mavenLocal()
}

jar {
    manifest {
        attributes 'Implementation-Title': project.name, 'Implementation-Version': project.version
    }
}

task printVersion {
    doLast {
        print project.version
    }
}

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "http://www.opensource.org/licenses/mit-license.php"
            distribution "repo"
        }
    }

    scm {
        url "https://github.com/hmcts/core-case-data-client"
    }
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'core-case-data-client'
            version project.version

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'core case data client')
                root.appendNode('name', 'Core case data client')
                root.appendNode('url', 'https://github.com/hmcts/core-case-data-client')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['Main']
    publish = true
    pkg {
        repo = 'hmcts-maven'
        name = 'core-case-data-client'
        userOrg = 'hmcts'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/hmcts/core-case-data-client'
        version {
            name = project.version
        }
    }
}

dependencies {

}

bootRepackage {
    enabled = false;
}
